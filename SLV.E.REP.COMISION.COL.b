*-----------------------------------------------------------------------------
* <Rating>-75</Rating>
*-----------------------------------------------------------------------------
SUBROUTINE SLV.E.REP.COMISION.COL(A.INFO)
*-----------------------------------------------------------------------------
*
*-----------------------------------------------------------------------------
* Modification History :
*-----------------------------------------------------------------------------
$INSERT I_COMMON
$INSERT I_EQUATE
$INSERT I_F.EB.SLV.COLECTOR
;*$INSERT I_F.EB.SLV.COLECTOR.TRANS.COL
;*$INSERT I_F.EB.SLV.COLECTOR.TRX.PAGO.COL
$INSERT I_F.EB.SLV.COLECTOR.TRX.PAGO
$INSERT I_ENQUIRY.COMMON
$INSERT I_F.EB.SLV.CATEGORIA.COLECTOR.COL 
$INSERT I_F.EB.SLV.COMM.PARAM
*-----------------------------------------------------------------------------

GOSUB INIT
GOSUB OPEN.FILE
GOSUB GET.FILTER.REPOR
GOSUB PROCCESS
RETURN

INIT:
;*CATALOGO DE COLECTORES
FN.TABLE.COLECTORES = 'F.EB.SLV.COLECTOR'
F.TABLE.COLECTORES = ''

;*REGISTROS DE PAGO DE COLECTORES
FN.TABLE.PAGO.COL = 'F.EB.SLV.COLECTOR.TRX.PAGO'
F.TABLE.PAGO.COL = ''

;*CATEGORIAS ACTIVAS DE COLECTORES
FN.TABLE.CATEGORIAS.COL = 'F.EB.SLV.CATEGORIA.COLECTOR.COL'
F.TABLE.CATEGORIAS.COL  = ''

FN.TABLE.COMISION.COL = 'F.EB.SLV.COMM.PARAM'
F.TABLE.COMISION.COL  = ''
RETURN

OPEN.FILE:
CALL OPF(FN.TABLE.COLECTORES,F.TABLE.COLECTORES)
CALL OPF(FN.TABLE.PAGO.COL,F.TABLE.PAGO.COL)
CALL OPF(FN.TABLE.CATEGORIAS.COL,F.TABLE.CATEGORIAS.COL)
CALL OPF(FN.TABLE.COMISION.COL,F.TABLE.COMISION.COL)
RETURN

;*OBTENEMOS LOS VALORES INGRESADOS EN LOS FILTROS DEL REPORTE
GET.FILTER.REPOR:
LOCATE "FECHA.REP" IN D.FIELDS<1> SETTING POS.FECHA THEN
    FECHA.FILTRO   = D.RANGE.AND.VALUE<POS.FECHA>
    FECHA.BUSQUEDA = SUBSTRINGS(FECHA.FILTRO,1,6)
END
RETURN

;*PROCEDEMOS A EJECUTAR TODA LA LÓGICA DEL REPORTE
PROCCESS:
;*OBTENEMOS LAS CATEGORIAS ACTIVAS
STMT.CATEGORIAS.COLECTOR = "SELECT ":FN.TABLE.CATEGORIAS.COL:" WITH ESTADO.CATEGORIA EQ 'ACTIVO'"
CALL EB.READLIST(STMT.CATEGORIAS.COLECTOR, ARRANGEMENT.LIST.CAT,'',NO.OF.RECS.CAT,Y.ARRANGEMENT.ERR1.CAT)

FOR I=1 TO NO.OF.RECS.CAT
;*OBTENEMOS EL DETALLE DE LA CATEGORIA QUE SE VA ITERANDO
CALL F.READ(FN.TABLE.CATEGORIAS.COL,ARRANGEMENT.LIST.CAT<I>,RS.DET.CATEGORY,F.TABLE.CATEGORIAS.COL,ERR.CAT)
CATEGORY.FIND = RS.DET.CATEGORY<EB.CAT.CATEGORIA.COLECTOR>
;*CONSULTAMOS TODOS LOS COLECTORES QUE PERTENECEN A DICHA CATEGORIA
STMT.COLECTORES.CATEGORIAS = "SELECT ":FN.TABLE.COLECTORES:" WITH ESTADO.CONVENIO EQ 'ACTIVO' AND CATEGORIA.COLECTOR EQ '":CATEGORY.FIND:"'"
CALL EB.READLIST(STMT.COLECTORES.CATEGORIAS,ARRANGEMENT.LIST.COL,'',NO.OF.RECS.COL,Y.ARRANGEMENT.ERR1.COL)
FOR CL = 1 TO NO.OF.RECS.COL
  
  ID.COLECTOR = ARRANGEMENT.LIST.COL<CL>  
  CANTIDAD.PAGOS = 0
  MONTO.MENSUAL  = 0
  
  ;*CONSULTAMOS EL DETALLE DEL COLECTOR
  CALL F.READ(FN.TABLE.COLECTORES,ID.COLECTOR,RS.DET.COLECTORES,F.TABLE.COLECTORES,ERR.COLECTORES)
  NOMBRE.COLECTOR = RS.DET.COLECTORES<EB.CL.NOMBRE.COLECTOR>
  
  ;*CONSULTAMOS LA COMISION PARAMETRIZADA POR COLECTOR EN EB.SLV.COMM.PARAM
  CALL F.READ(FN.TABLE.COMISION.COL,ID.COLECTOR,RS.DET.COMISION,F.TABLE.COMISION.COL,ERR.COMISION)
  COMISION.COLECTOR = RS.DET.COMISION<EB.SLV71.AMOUNT.COMM.CL,1>
  
  ;*OBTENEMOS TODOS LOS PAGOS DE CADA COLECTOR QUE SE VA ITERANDO.
  STMT.PAGOS.COLECTOR = "SELECT ":FN.TABLE.PAGO.COL:" WITH FECHA LIKE %":FECHA.BUSQUEDA:"% AND ID.COLECTOR EQ ":ID.COLECTOR
  CALL EB.READLIST(STMT.PAGOS.COLECTOR, ARRANGEMENT.LIST.PAGO.COL,'',NO.OF.RECS.PAGO.COL,Y.ARRANGEMENT.ERR1.PG.COL)
  
  ;*COMENZAMOS A RECORRER CADA PAGO
  FOR PG=1 TO NO.OF.RECS.PAGO.COL  
  ID.PAGO.COLECTOR = ARRANGEMENT.LIST.PAGO.COL<PG>
  CALL F.READ(FN.TABLE.PAGO.COL,ID.PAGO.COLECTOR,RS.DET.PAGO.CL,F.TABLE.PAGO.COL,ERR.PAGO.COL)
  MONTO.MENSUAL += RS.DET.PAGO.CL<EB.SLV54.MONTO>
  CANTIDAD.PAGOS++
  NEXT PG
    
  IF CANTIDAD.PAGOS NE 0 THEN
     GOSUB CALCULO.COMISION
     GOSUB CALCULO.IVA
     A.INFO<-1> = CANTIDAD.PAGOS:'*':NOMBRE.COLECTOR:'*':MONTO.MENSUAL:'*':COMISION.REPORTE:'*':IVA.COLECTOR
  END
  
NEXT CL ;*FIN DE ITERACION POR COLECTOR

NEXT I  ;*FIN DE ITERANCION POR CATEGORIA 
RETURN

CALCULO.COMISION:
    ;*CRT '>>':ID.COLECTOR
    ;*CRT CANTIDAD.PAGOS :'*':COMISION.COLECTOR 
    COMISION.REPORTE = CANTIDAD.PAGOS * COMISION.COLECTOR
RETURN

CALCULO.IVA:
    IVA.COLECTOR = COMISION.REPORTE * 0.13
RETURN
END
