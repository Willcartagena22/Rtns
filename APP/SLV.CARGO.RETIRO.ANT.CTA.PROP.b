*-----------------------------------------------------------------------------
* <Rating>-72</Rating>
*-----------------------------------------------------------------------------
SUBROUTINE SLV.CARGO.RETIRO.ANT.CTA.PROP
*-----------------------------------------------------------------------------
*
*-----------------------------------------------------------------------------
* Modification History :
*-----------------------------------------------------------------------------
    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.EB.SLV.GLOBAL.PARAM
    $INSERT I_F.FUNDS.TRANSFER
    $INSERT I_F.ACCOUNT
    $INSERT I_F.EB.SLV.ACC.PROPOSITO.ID
    $INSERT I_F.EB.SLV.REL.PROD.CONVENIO
*-----------------------------------------------------------------------------
    
    VERSION = APPLICATION:PGM.VERSION
    
    IF VERSION NE 'FUNDS.TRANSFER,CARGO.AUTOMATICO.CTA.PROPOSITO' THEN
       GOSUB INIT
       GOSUB OPEN.FILE
       GOSUB VAR.GLOBAL.PROGRAM
       GOSUB PROCESS
       
    END
    
    RETURN

INIT:
    app.global.prm.fn   = 'F.EB.SLV.GLOBAL.PARAM'
    app.global.prm.f    = ''
    app.account.fn      = 'F.ACCOUNT'
    app.account.f       = ''
    app.acc.prop.id.fn  = 'F.EB.SLV.ACC.PROPOSITO.ID'
    app.acc.prop.id.f   = ''
    app.convenio.cta.fn = 'F.EB.SLV.REL.PROD.CONVENIO'
    app.convenio.cta.f  = ''
    RETURN

OPEN.FILE:
    CALL OPF(app.global.prm.fn,app.global.prm.f)
    CALL OPF(app.account.fn,app.account.f)
    CALL OPF(app.acc.prop.id.fn,app.acc.prop.id.f)
    CALL OPF(app.convenio.cta.fn,app.convenio.cta.f)
    RETURN

VAR.GLOBAL.PROGRAM:
    EQU ID.MONTO.CARGO  TO 'SLV.CARGO.RETIRO.CTA.PROP'
    EQU CATEGORY.NUMBER TO '6017'
    EQU CTA.CREDIT.CRG  TO 'SLV.CTA.CREDIT.CARGO.ANTICIPADO.PROP'
    EQU ORDERING.BANK   TO 'CARGO.RETIRO.ANTICIPADO'
    EQU PARAM.OFS.ID    TO 'OFS.CARGO.AUTOMATICO.PROP'
    EQU ESTADO.VALIDO   TO 'VIGENTE'
    EQU FT.CURRENCY     TO 'USD'
    EQU TRX.TYPE        TO 'AC70'
    EQU PRM.MONTO.MIN.COM TO 'ID.PRM.MONTO.MIN.COM'
    RETURN

PROCESS:
;*OBTENEMOS LA CUENTA DE DEBITO DE LA APLICACION FUNDS.TRANSFER
    ACCOUNT.NUMBER = R.NEW(FT.DEBIT.ACCT.NO)
;*OBTENEMOS EL DETALLE DE ESA CUENTA
    CALL F.READ(app.account.fn,ACCOUNT.NUMBER,ACC.R,app.account.f,ACC.ERR)
;*OBTENEMOS EL CATEGORY DE LA CUENTA DEBITO DE LA APLICACION FUNDS.TRANSFER
    CATEGORY.ACCOUNT = ACC.R<AC.CATEGORY>

    IF CATEGORY.ACCOUNT EQ CATEGORY.NUMBER THEN
        ;*OBTENEMOS EL MONTO DEL CARGO POR RETIRO ANTICIPIDADO CONFIGURADO
        CALL F.READ(app.global.prm.fn,ID.MONTO.CARGO,GLOBAL.R,app.global.prm.f,GLOBAL.ERR)
        PORCENTAJE.RETIRO.ANTICIPADO = GLOBAL.R<EB.SLV39.VALOR.PARAM>
        MONTO.RETIRO.ANTICIPADO.FT   = R.NEW(FT.DEBIT.AMOUNT) * PORCENTAJE.RETIRO.ANTICIPADO / 100
        MONTO.RETIRO.ANTICIPADO      = FMT(MONTO.RETIRO.ANTICIPADO.FT ,'R2')
        
        ;*OBTENEMOS EL MONTO MINIMO PARA EL COBRO DE COMISION POR RETIRO ANTICIPADO DE EFECTIVO EN UNA CUENTA SIMPLIFICADA     
        CALL F.READ(app.global.prm.fn,PRM.MONTO.MIN.COM,GLOBAL.M.R,app.global.prm.f,GLOBAL.ERR)
        MONTO.MINIMO.COMISION = GLOBAL.M.R<EB.SLV39.VALOR.PARAM>
        
        IF MONTO.RETIRO.ANTICIPADO < MONTO.MINIMO.COMISION THEN
            MONTO.RETIRO.ANTICIPADO = MONTO.MINIMO.COMISION
        END
        
        ;*MONTO.RETIRO.ANTICIPADO
        ;*OBTENEMOS LA CUENTA DE CREDITO PARA ABONAR LA COMISION
        CALL F.READ(app.global.prm.fn,CTA.CREDIT.CRG,GLOBAL.R.ACC,app.global.prm.f,GLOBAL.ERR.ACC)
        CREDIT.ACC.CARGO        = GLOBAL.R.ACC<EB.SLV39.VALOR.PARAM>
        ;*OBTENEMOS EL ID DEL CONVENIO DE LA CUENTA DEBITO DE LA APLICACION FUNDS.TRANSFER
        CALL F.READ(app.acc.prop.id.fn,ACCOUNT.NUMBER,ACC.ID.R,app.acc.prop.id.f,ACC.ID.R)
        CONVENIO.ID.ACC.PROP    = ACC.ID.R<EB.SLV39.PROD.CONVENIO.ID>
        ;*OBTENEMOS EL DETALLE DEL CONVENIO DE LA CUENTA DEBITO DE LA APLICACION FUNDS.TRANSFER
        CALL F.READ(app.convenio.cta.fn,CONVENIO.ID.ACC.PROP,CONVENIO.R,app.convenio.cta.f,CONVENIO.ERR)
        ;*OBTENEMOS EL ESTADO DEL CONVENIO DE LA CUENTA DEBITO DE LA APLICACION FUNDS.TRANSFER
        ESTADO.CONVENIO         = CONVENIO.R<EB.SLV67.STATUS>

        ;*IF ESTADO.CONVENIO EQ ESTADO.VALIDO THEN
            ;*CAMBIAMOS EL ESTADO AL CONVENIO A CANCELADO POR RETIRO ANTICIPADO DEL DINERO
            GOSUB CHANGE.STATUS.AGREEMENT
            ;*PROCEDEMOS A REALIZAR EL OFS PARA EL CARGO POR RETIRO ANTICIPADO
            GOSUB SEND.OFS.CRG.ACC.PROP
        ;*END

    END

    RETURN

CHANGE.STATUS.AGREEMENT:
    CONVENIO.R<EB.SLV67.STATUS> = 'CANCELADO'
    CONVENIO.R<EB.SLV67.MOTIVO> = 'Cancelacion de Convenio por retiro anticipado en la cuenta ':ACCOUNT.NUMBER
    CALL F.WRITE (app.convenio.cta.fn, CONVENIO.ID.ACC.PROP, CONVENIO.R)
    RETURN

SEND.OFS.CRG.ACC.PROP:
    TRANS.ID     = ''
    Y.OUT        = ''
    ID.PARAM.OFS = PARAM.OFS.ID
    R.FT<FT.DEBIT.ACCT.NO>    = ACCOUNT.NUMBER
    R.FT<FT.CREDIT.ACCT.NO>   = CREDIT.ACC.CARGO
    R.FT<FT.DEBIT.CURRENCY>   = FT.CURRENCY
    R.FT<FT.DEBIT.AMOUNT>     = MONTO.RETIRO.ANTICIPADO
    R.FT<FT.ORDERING.BANK>    = ORDERING.BANK
    R.FT<FT.TRANSACTION.TYPE> = TRX.TYPE
    CALL SLV.OFS.UTIL.OL.TRX(TRANS.ID,R.FT,ID.PARAM.OFS,Y.OUT)
   RETURN

    END
